plugins {
    id 'java'
}

group 'net.insprill'
version this.getFullVersion()

allprojects {
    group "${gradle.rootProject.group}"
    version "${gradle.rootProject.version}"

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    }
}

jar {
    enabled = false
}

task copyJars() {
    doLast {
        copy {
            from("dist/build/libs")
            into("build/libs")
        }
        if (new File("server/plugins").exists()) {
            copy {
                from("dist/build/libs")
                into("server/plugins")
            }
        }
    }
}

build {
    enabled = false
    dependsOn(":dist:shadowJar").finalizedBy(copyJars)
}

def getFullVersion() {
    var version = project.property("version");
    if (version.contains('-SNAPSHOT')) {
        return version + '+rev.' + 'git rev-parse --verify --short HEAD'.execute().text.trim()
    } else {
        return version
    }
}
